#Include 'Totvs.ch'
#Include 'Protheus.ch'
#Include 'Topconn.ch'

#DEFINE ENTER CHR(13)+CHR(10)
#DEFINE REPLACE StrTran
#DEFINE SUBSTRING Substr

/*{Protheus.doc} User Function Projeto02
    (Projeto de Tela com 3 Abas e varios elementos + gravacaoo dos dados no banco)
    @type  Function
    @author Flavio Lisbos
    @since 27/04/2021
    @see https://tdn.totvs.com/display/tec/Construtor+TGet%3ANew
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=24347085
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=22479526
    @see https://tdn.totvs.com/display/tec/TGet%3AcF3
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=23889301
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=24347041
    @see https://tdn.totvs.com/display/tec/TCheckBox
    @see https://tdn.totvs.com/display/tec/TRadMenu
    @see https://tdn.totvs.com/display/tec/TComboBox
    @see https://tdn.totvs.com/display/tec/TListBox
    @see https://tdn.totvs.com/display/tec/TGroup
    @see https://tdn.totvs.com/display/tec/TBitmap
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=23889301
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=24347041
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=23889268
    @see https://tdn.totvs.com/display/tec/DBSelectArea
    @see https://tdn.totvs.com/display/tec/DBSetOrder
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=27675626
    @see https://tdn.engpro.totvs.com.br/pages/viewpage.action?pageId=23889301
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=23889268
    @see https://tdn.totvs.com/pages/viewpage.action?pageId=24347000
    @see https://tdn.totvs.com/display/tec/DBCloseArea
*/
User Function CadProt()

Private oDlg1
Private cTituloJanela  := "CADASTRO DE PROTHEUZEIRO"

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%                                                                                                                                                         %%%%%               
%%%%                                     ###                      Declarar as variaveis do Folder/Pasta              ###                                     %%%%%
%%%%                                                                                                                                                         %%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/


Private oFolder
Private aFolder

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%                                                           Variaveis do Folder/Aba 01 (Dados Pessoais)                                                  %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

Private oSayCod
Private oGetCod
Private cGetCod

Private oSayNome
Private oGetNome
Private cGetNome

Private oSayNreduz
Private oGetNreduz
Private cGetNreduz

Private oSayDtNasc
Private oGetDtNasc
Private dGetDtNasc

Private oSayDDD
Private oGetDDD
Private cGetDDD

Private oSayNCel
Private oGetNCel
Private cGetNCel

Private oGroupSexo // Agrupador de elementos que recebera os RadioButtons de Sexo

Private aSexo      := {"Masculino","Feminino"}
Private nSexo      := 1
Private oRadioSexo


Private oSayCPF
Private oGetCPF
Private cGetCPF


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%                                                           Comeca a construir a Tela                                                                    %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/


oDlg1       := TDialog():New(0,0,720,1280,cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)
aFolder     := {"Dados Pessoais","Endereco","Outros"}
oFolder     := TFolder():New(5,5,aFolder,aFolder,oDlg1,,,,.T.,.T.,600,200)

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%                                            Elementos visuais que compoem o FOLDER 01 (Dados Pessoais)                                                  %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

oSayCod     := TSay():New(01,10,{||"Codigo"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCod     := Space(6)
oGetCod     := TGet():New(10,05,{|u| IIF(Pcount()>0,cGetCod :=u,cGetCod)},oFolder:aDialogs[1],060,010,"@E 999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCod,,,, )
oGetCod:cF3 := "SZ1"
oGetCod:cPlaceHold := "Codigo"

oSayNome    := TSay():New(25,10,{||"Nome"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetNome    := Space(30)
oGetNome    := TGet():New(35,05,{|u| IIF(Pcount()>0,cGetNome :=u,cGetNome)},oFolder:aDialogs[1],095,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,, )

oSayNreduz  := TSay():New(25,120,{||"Nome Reduzido"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetNreduz  := Space(20)
oGetNreduz  := TGet():New(35,115,{|u| IIF(Pcount()>0,cGetNreduz :=u,cGetNreduz)},oFolder:aDialogs[1],095,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNreduz,,,, )

oSayDtNasc  := TSay():New(25,225,{||"Data de Nascimento"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
dGetDtNasc  := Date() // Funcao que tras a data atual
oGetDtNasc  := TGet():New(35,220,{|u| IIF(Pcount()>0,dGetDtNasc :=u,dGetDtNasc)},oFolder:aDialogs[1],060,010,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,"",,,, )

oSayDDD     := TSay():New(25,315,{||"DDD"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetDDD     := Space(2)
oGetDDD     := TGet():New(35,310,{|u| IIF(Pcount()>0,cGetDDD :=u,cGetDDD)},oFolder:aDialogs[1],10,010,"@E(99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDD,,,,.T. )

oSayNCel    := TSay():New(25,345,{||"Numero Celular"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetNCel    := Space(11)
oGetNCel    := TGet():New(35,340,{|u| IIF(Pcount()>0,cGetNCel :=u,cGetNCel)},oFolder:aDialogs[1],095,010,"@E 9-9999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNCel,,,, )

oGroupSexo  := TGroup():New(30,475,75,565,"Sexo",oFolder:aDialogs[1],,,.T.) //Preste atencao no dimensionamento/tamanho do elemento. este e diferente dos outros
oRadioSexo  := TRadMenu():New(45,485,aSexo,{|u| IIF(PCount()>0,nSexo  := u,nSexo)},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

oSayCPF     := TSay():New(55,10,{||"CPF"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCPF     := Space(14)
oGetCPF     := TGet():New(65,05,{|u| IIF(Pcount()>0,cGetCPF :=u,cGetCPF)},oFolder:aDialogs[1],095,010,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCPF,,,, )


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%                                           Elementos visuais que compoem o FOLDER 02 (Endereco)                                                         %%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

Private oSayEndereco
Private cGetEndereco
Private oGetEndereco

Private oSayBairro
Private cGetBairro
Private oGetBairro

Private oSayCEP
Private cGetCEP
Private oGetCEP

Private oSayCidade
Private cGetCidade
Private oGetCidade

Private oSayEstado
Private aEstado     := {"AC","AL","AM","AP","BA","CE","DF","ES","EX","GO","MA","MG","MS","MT","PA","PB","PE","PI","PR","RJ","RN","RO","RR","RS","SC","SE","SP","TO"}
Private cEstado     := aEstado[2]
Private cGetEstado
Private oGetEStado

Private oSayImovel
Private aImovel     := {"Casa","Apartamento","Outros"}
Private cImovel     := aImovel[1]
Private cGetImovel
Private oGetImovel


oSayEndereco   := TSay():New(01,10,{||"Endereco"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetEndereco   := Space(30)
oGetEndereco   := TGet():New(10,05,{|u| IIF(Pcount()>0,cGetEndereco :=u,cGetEndereco)},oFolder:aDialogs[2],095,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEndereco,,,, )

oSayBairro     := TSay():New(01,120,{||"Bairro"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetBairro     := Space(20)
oGetBairro     := TGet():New(10,115,{|u| IIF(Pcount()>0,cGetBairro :=u,cGetBairro)},oFolder:aDialogs[2],095,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBairro,,,, )

oSayCEP        := TSay():New(01,218,{||"CEP"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCEP        := Space(10)
oGetCEP        := TGet():New(10,215,{|u| IIF(Pcount()>0,cGetCEP :=u,cGetCEP)},oFolder:aDialogs[2],095,010,"@E 99.999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEP,,,, )

oSayCidade     := TSay():New(01,335,{||"Cidade"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cGetCidade     := Space(30)
oGetCidade     := TGet():New(10,330,{|u| IIF(Pcount()>0,cGetCidade :=u,cGetCidade)},oFolder:aDialogs[2],095,010,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCidade,,,, )

oSayEstado     := TSay():New(01,450,{||"UF"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboEstado   := TComboBox():New(010,455,{|u| IIF(Pcount()>0,cEstado :=u,cEstado)},aEstado,050,020,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cEstado')

oSayImovel     := TSay():New(30,10,{||"Tipo de Imovel"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboImovel   := TComboBox():New(040,05,{|u| IIF(Pcount()>0,cImovel :=u,cImovel)},aImovel,095,025,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                           Elementos visuais que compoem o FOLDER 03 (Outros)                                                         %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Private oSayEstCivil
Private aEstCivil     := {"Solteiro","Namorando","Casado","Viuvo","Divorciado","Falecido"}
Private cEstCivil     := aEstCivil[1]
Private cGetEstCivil
Private oGetEStCivil

Private oGroupPet

Private aPet        := {"Sim","Nao"}
Private nPet        := aPet[1]
Private oRadioPet

Private oGroupLinguagens

Private oCheckAdvpl
Private oCheckCSHARP
Private oCheckSQL
Private oCheckPython

Private lCheckAdvpl   := .F.
Private lCheckCSHARP  := .F.
Private lCheckSQL     := .F.
Private lCheckPython  := .F.

Private oSayButtons
Private oButtonMarcarTodos
Private oButtonDesMarcarTodos
Private oButtonMostraEscolha

Private oSayObservacao
Private cObservacao
Private oTMultigetObservacao

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                    Variaveis de elementos visuais que estao fora do folder  (BitMaps)                                                %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Private oButtonIncluir
Private oButtonAlterar
Private oButtonConsultar
Private oButtonExcluir

Private oBmpIncluir
Private oBmpAlterar
Private oBmpConsultar
Private oBmpExcluir

oSayEstCivil            := TSay():New(01,10,{||"Estado Civil"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
oComboEstCivil          := TComboBox():New(010,05,{|u| IIF(Pcount()>0,cEstCivil :=u,cEstCivil)},aEstCivil,050,020,oFolder:aDialogs[3],,,,,,.T.,,,,,,,,,'cEstCivil')

oGroupPet               := TGroup():New(01,80,70,120,"Possui Pet?",oFolder:aDialogs[3],,,.T.) //Preste atencao no dimensionamento/tamanho do elemento. este e diferente dos outros
oRadioPet               := TRadMenu():New(15,85,aPet,{|u| IIF(PCount()>0,nPet  := u,nPet)},oFolder:aDialogs[3],,,,,,,,30,20,,,,.T.)

oGroupLinguagens        := TGroup():New(01,150,70,250,"Linguagem que Domina",oFolder:aDialogs[3],,,.T.) //Preste atencao no dimensionamento/tamanho do elemento. este e diferente dos outros

oCheckAdvpl             := TCheckBox():New(15,160,'ADVPL',{|| lCheckAdvpl},oFolder:aDialogs[3],100,210,,{|| lCheckAdvpl  := !lCheckAdvpl},,,,,,,,.T.,,,)

oCheckCSHARP            := TCheckBox():New(25,160,'CSHARP',{|| lCheckCSHARP},oFolder:aDialogs[3],100,210,,{|| lCheckCSHARP  := !lCheckCSHARP},,,,,,,,.T.,,,)

oCheckSQL               := TCheckBox():New(35,160,'SQL',{|| lCheckSQL},oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
oCheckSQL:bSetGet       := {|| lCheckSQL}
oCheckSQL:bLClicked     := {|| lCheckSQL  := !lCheckSQL}

oCheckPython            := TCheckBox():New(45,160,'Python ',{|| lCheckPython },oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
oCheckPython:bSetGet    := {|| lCheckPython}
oCheckPython:bLClicked  := {|| lCheckPython  := !lCheckPython}

oSayButtons             := TSay():New(100,15,{||"Botoes para Marcar as Opcoes"},oFolder:aDialogs[3],,,,,,.T.,CLR_RED,CLR_WHITE,300,20)

oButtonMarcarTodos      := TButton():New(120,10, "Marcar Todos"   ,oFolder:aDialogs[3],{|| MarcarTodos()}, 60,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonDesMarcarTodos   := TButton():New(120,80, "Desmarcar Todos",oFolder:aDialogs[3],{|| Desmarcar()}, 60,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonMostraEscolha    := TButton():New(120,150, "Mostrar Escolha",oFolder:aDialogs[3],{|| CheckOptions()}, 60,15,,,.F.,.T.,.F.,,.F.,,,.F.)

oSayObservacao          := TSay():New(01,270,{|| "Observacao"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
cObservacao             := "Digite aqui as caracteristicas de Protheuzeiro que te definem:"
oTMultigetObservacao    := TMultiget():New(10,270, {|u| if(PCount()>0,cObservacao  := u, cObservacao)},oFolder:aDialogs[3],200,100,,,,,,.T.)


//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                               Elementos visuais que estao fora do folder  (BitMaps)                                                  %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

oBmpIncluir              := TBitmap():New(270,30,142,142,,"H:\TOTVS\protheus_data\system\incluirprotheuzeiro.bmp",.T.,oDlg1,{|| IncluirSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpIncluir:lAutoSize    := .T.
oBmpAlterar              := TBitmap():New(270,90,142,142,,"H:\TOTVS\protheus_data\system\alterarprotheuzeiro.bmp",.T.,oDlg1,{|| AlterarSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpAlterar:lAutoSize    := .T.
oBmpConsultar            := TBitmap():New(270,150,142,142,,"H:\TOTVS\protheus_data\system\consultarprotheuzeiro.bmp",.T.,oDlg1,{|| BuscarSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpConsultar:lAutoSize  := .T.
oBmpExcluir              := TBitmap():New(270,210,142,142,,"H:\TOTVS\protheus_data\system\excluirprotheuzeiro.bmp",.T.,oDlg1,{|| ExcluirSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpExcluir:lAutoSize    := .T.
oBmpLimpar               := TBitmap():New(270,330,142,142,,"H:\TOTVS\protheus_data\system\limpartela.bmp",.T.,oDlg1,{||LimpaTela()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpLimpar:lAutoSize     := .T.
oBmpSair                 := TBitmap():New(270,490,142,142,,"H:\TOTVS\protheus_data\system\sair.bmp",.T.,oDlg1,{||oDlg1:End()},,.F.,.F.,,,.F.,,.T.,,.F.)
oBmpSair:lAutoSize       := .T.


oButtonIncluir           := TButton():New(300, 20,  "Incluir",     oDlg1,{|| IncluirSZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonAlterar           := TButton():New(300, 80,  "Alterar",     oDlg1,{|| AlterarSZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonConsultar         := TButton():New(300, 140, "Consultar",   oDlg1,{|| BuscarSZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonExcluir           := TButton():New(300, 200, "Excluir",     oDlg1,{|| ExcluirSZ1()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonLimpar            := TButton():New(300, 320, "Limpar",      oDlg1,{|| LimpaTela()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
oButtonSair              := TButton():New(300, 480, "Sair",        oDlg1,{|| oDlg1:End()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)

oDlg1:Activate(,,,.T.)

Return

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                          Funcao Para Mostrar os CheckBox Marcados  (CheckOption)                                                     %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function CheckOptions()

if lCheckAdvpl .AND. (lCheckCSHARP .OR. lCheckPython .OR. lCheckSQL) = .F.
    MsgInfo("Somente o Advpl esta marcada")
elseif lCheckCSHARP .AND. (lCheckAdvpl .OR. lCheckPython .OR. lCheckSQL) = .F.
    MsgInfo("Somente o CSHARP esta marcada")
elseif lCheckSQL .AND. (lCheckAdvpl .OR. lCheckPython .OR. lCheckCSHARP) = .F.
    MsgInfo("Somente SQL esta marcada")
elseif lCheckPython .AND. (lCheckAdvpl .OR. lCheckCSHARP .OR. lCheckSQL) = .F.
    MsgInfo("Somente Python esta marcada")
elseif lCheckAdvpl .AND. lCheckCSHARP .AND. (lCheckPython .OR. lCheckSQL) =.F.
    MsgInfo("As opcoes ADVPL e CSHARP estao marcadas")
elseif lCheckAdvpl .AND. lCheckSQL .AND. (lCheckPython .OR. lCheckCSHARP) = .F.
    MsgInfo("As opcoes ADVPL e SQL estao marcadas")
elseif lCheckAdvpl .AND. lCheckPython .AND. (lCheckCSHARP .OR. lCheckSQL) = .F.
    MsgInfo("As opcoes ADVPL e Python estao marcadas")
elseif lCheckCSHARP .AND. lCheckSQL .AND. (lCheckPython .OR. lCheckAdvpl) = .F.
    MsgInfo("As opcoes CSHARP e SQL estao marcadas")   
elseif lCheckCSHARP .AND. lCheckPython .AND. (lCheckSQL .OR. lCheckAdvpl) =.F.
    MsgInfo("As opcoes CSHARP e Python estao marcadas")
elseif lCheckPython .AND. lCheckSQL .AND. (lCheckAdvpl .OR. lCheckCSHARP) = .F.
    MsgInfo("As opcoes Python e SQL estao marcadas")
elseif lCheckPython .AND. lCheckAdvpl .AND. lCheckCSHARP .AND. !lCheckSQL
    MsgInfo("As opcoes ADVPL, CSHARP e Python estao marcadas")
elseif !lCheckPython .AND. lCheckAdvpl .AND. lCheckCSHARP .AND. lCheckSQL
    MsgInfo("As opcoes ADVPL, CSHARP e SQL estao marcadas")
elseif lCheckPython .AND. !lCheckAdvpl .AND. lCheckCSHARP .AND. lCheckSQL
    MsgInfo("As opcoes CSHARP, Python e SQL estao marcadas")    
elseif lCheckPython .AND. lCheckAdvpl .AND. !lCheckCSHARP .AND. lCheckSQL
    MsgInfo("As opcoes ADVPL, Python e SQL estao marcadas")  
elseif lCheckPython .AND. lCheckAdvpl .AND. lCheckCSHARP .AND. lCheckSQL 
    MsgInfo("Todas as opcoes estao marcadas")
else
    MsgInfo("Todas as opcoes estao desmarcadas")
endif
    
Return 

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                          Funcao Para Marcar Todos CheckBox (MarcarTodos)                                                             %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


Static Function MarcarTodos()

lCheckAdvpl   := .T.
lCheckCSHARP  := .T. 
lCheckSQL     := .T.
lCheckPython  := .T.

oCheckAdvpl:CtrlRefresh()
oCheckCSHARP:CtrlRefresh()
oCheckSQL:CtrlRefresh()
oCheckPython:CtrlRefresh()
    
Return 

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                          Funcao Para Desmarcar Todos CheckBox   (Desmarcar)                                                          %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function Desmarcar()

lCheckAdvpl   := .F.
lCheckCSHARP  := .F.
lCheckSQL     := .F.
lCheckPython  := .F.

oCheckAdvpl:Refresh()
oCheckCSHARP:Refresh()
oCheckSQL:Refresh()
oCheckPython:Refresh()
    
Return 


//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                           Funcao para incluir registros na tabela SZ1                                                                %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



Static Function IncluirSZ1()

DBSelectArea("SZ1")
DBSetOrder(1)

if DbSeek(xFilial("SZ1")+cGetCod)
    MsgInfo("O Codigo Escolhido ja existe","Inclusao Negada!")
else
    
    if (!EMPTY(cGetCod) .AND. !EMPTY(cGetNome) .AND. !EMPTY(cGetNreduz) .AND. !EMPTY(cGetDDD) .AND. !EMPTY(cGetNCel) .AND. !EMPTY(cGetCPF))
            
         Reclock("SZ1",.T.)

          //Dados 
          SZ1->Z1_FILIAL    := xFilial("SZ1") //Preenchera branco/dois espacos vazios, pois apensar de estarmos logado da filial 01, esta tabela e compartilhada.
          SZ1->Z1_COD       := cGetCod
          SZ1->Z1_NOME      := cGetNome
          SZ1->Z1_NOMERED   := cGetNreduz
          SZ1->Z1_DATNASC   := dGetDtNasc
          SZ1->Z1_DDDCEL    := REPLACE(REPLACE(cGetDDD,"(",""),")","")
          SZ1->Z1_CELULAR   := REPLACE(cGetNCel,"-","")
          SZ1->Z1_SEXO      := cValToChar(nSexo) // 1 Para Masculino - 2 para feminino
          /*SZ1->Z1_SEXO    := IIF(aSexo[nSexo] = "Masculino",1,2) - outra forma de fazer*/ 
          SZ1->Z1_CPF       := REPLACE(REPLACE(cGetCPF,".",""),"-","")

          //Endereco
          SZ1->Z1_ENDEREC   := cGetEndereco
          SZ1->Z1_BAIRRO    := cGetBairro
          SZ1->Z1_CEP       := REPLACE(REPLACE(cGetCEP,".",""),"-","")
          SZ1->Z1_CIDADE    := cGetCidade
          SZ1->Z1_ESTADO    := cEstado
          SZ1->Z1_TIPOIMO   := cImovel

          //Outros   
          SZ1->Z1_ESTCIVI   := cEstCivil
          SZ1->Z1_TEMPET    := cValToChar(nPet)
          SZ1->Z1_LADVPL    := IIF(lCheckAdvpl,"1","2")  
          SZ1->Z1_CSHARP    := IIF(lCheckCSHARP,"1","2") 
          SZ1->Z1_SQL       := IIF(lCheckSQL,"1","2")    
          SZ1->Z1_PYTHON    := IIF(lCheckPython,"1","2") 
          SZ1->Z1_MEMO      := cObservacao


        MsUnlock()
        

          if SZ1->Z1_SEXO = "1"
             MsgInfo("Protheuzeiro incluido com Sucesso!", "Sucesso")
          else
             MsgInfo("Protheuzeira incluida com Sucesso!", "Sucesso")
          endif

        
    else
       Alert("Existe campos em branco, por favor verifique!!!")
    endif

    LimpaTela()

endif        

DbCloseArea()

Return 


//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                           Funcao para buscar registros na tabela SZ1                                                              %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function BuscarSZ1()

DBSelectArea("SZ1")
DBSetOrder(1)


if dbSeek(xFilial("SZ1")+cGetCod)
        if SZ1->Z1_SEXO = "1"
            MsgInfo("Protheuzeiro localizado", "Sucesso")
        else
            MsgInfo("Protheuzeira localizada", "Sucesso")
            
        endif

              //Dados                        
              cGetNome      :=  SZ1->Z1_NOME       
              cGetNreduz    :=  SZ1->Z1_NOMERED    
              dGetDtNasc    :=  SZ1->Z1_DATNASC    
              cGetDDD       :=  SZ1->Z1_DDDCEL     
              cGetNCel      :=  SUBSTRING(SZ1->Z1_CELULAR,1,1)+"-"+SUBSTRING(SZ1->Z1_CELULAR,2,4)+"-"+SUBSTRING(SZ1->Z1_CELULAR,6,4)
              nSexo         :=  Val(SZ1->Z1_SEXO) //Convert de caracter para numero       
              cGetCPF       :=  SUBSTR(SZ1->Z1_CPF,1,3)+"."+SUBSTRING(SZ1->Z1_CPF,4,3)+"."+SUBSTRING(SZ1->Z1_CPF,7,3)+"-"+SUBSTRING(SZ1->Z1_CPF,10,2)        

              //Endereco
              cGetEndereco  := SZ1->Z1_ENDEREC    
              cGetBairro    := SZ1->Z1_BAIRRO     
              cGetCEP       := SUBSTRING(SZ1->Z1_CEP,1,2)+"."+SUBSTRING(SZ1->Z1_CEP,3,3)+"-"+SUBSTRING(SZ1->Z1_CEP,6,3)        
              cGetCidade    := SZ1->Z1_CIDADE     
              cEstado       := SZ1->Z1_ESTADO     
              cImovel       := SZ1->Z1_TIPOIMO     

            //Outros   
              cEstCivil     := SZ1->Z1_ESTCIVI    
              nPet          := Val(SZ1->Z1_TEMPET) //Convert de caracter para numero    
              lCheckAdvpl   := IIF(SZ1->Z1_LADVPL = "1",.T.,.F.)      
              lCheckCSHARP  := IIF(SZ1->Z1_CSHARP = "1",.T.,.F.)      
              lCheckSQL     := IIF(SZ1->Z1_SQL = "1",.T.,.F.)         
              lCheckPython  := IIF(SZ1->Z1_PYTHON = "1",.T.,.F.)      
              cObservacao   := SZ1->Z1_MEMO       

else
             Alert("Protheuzeiro(a) nao localizado(a)", "Sucesso")
endif

DbCloseArea()

Return



//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                           Funcao para alterar os registros na tabela SZ1                                                             %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function AlterarSZ1()

if (EMPTY(cGetCod) .OR. EMPTY(cGetNome) .OR. EMPTY(cGetNreduz) .OR. EMPTY(cGetDDD) .OR. EMPTY(cGetNCel) .OR. EMPTY(cGetCPF))
           Alert("Existe campos vazios, por favor verifique!")
elseif MSGYESNO("Deseja mesmo alterar o registro de "+AllTrim(cGetNome)+"?","ATENCAO")


DBSelectArea("SZ1")
DBSetOrder(1)


 if DbSeek(xFilial("SZ1")+cGetCod)

    Reclock("SZ1",.F.)

      //Dados 
      SZ1->Z1_NOME      := cGetNome
      SZ1->Z1_NOMERED   := cGetNreduz
      SZ1->Z1_DATNASC   := dGetDtNasc
      SZ1->Z1_DDDCEL    := REPLACE(REPLACE(cGetDDD,"(",""),")","")
      SZ1->Z1_CELULAR   := REPLACE(cGetNCel,"-","")
      SZ1->Z1_SEXO      := cValToChar(nSexo) // 1 Para Masculino - 2 para feminino
      SZ1->Z1_CPF       := REPLACE(REPLACE(cGetCPF,".",""),"-","")

      //Endereco
      SZ1->Z1_ENDEREC   := cGetEndereco
      SZ1->Z1_BAIRRO    := cGetBairro
      SZ1->Z1_CEP       := REPLACE(REPLACE(cGetCEP,".",""),"-","")
      SZ1->Z1_CIDADE    := cGetCidade
      SZ1->Z1_ESTADO    := cEstado
      SZ1->Z1_TIPOIMO   := cImovel

      //Outros   
      SZ1->Z1_ESTCIVI   := cEstCivil
      SZ1->Z1_TEMPET    := cValToChar(nPet)
      SZ1->Z1_LADVPL    := IIF(lCheckAdvpl,"1","2")  
      SZ1->Z1_CSHARP    := IIF(lCheckCSHARP,"1","2") 
      SZ1->Z1_SQL       := IIF(lCheckSQL,"1","2")    
      SZ1->Z1_PYTHON    := IIF(lCheckPython,"1","2") 
      SZ1->Z1_MEMO      := cObservacao

    MsUnlock()
        if SZ1->Z1_SEXO = "1"
            MsgInfo("Protheuzeiro Alterado", "Sucesso")
        else
            MsgInfo("Protheuzeira Alterada", "Sucesso")
        endif

else 
    Alert("Exclusao abortada pelo usuario","Atencao")          


  endif

endif

Return

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                           Funcao para excluir os registros na tabela SZ1                                                             %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function ExcluirSZ1()

if (EMPTY(cGetCod) .OR. EMPTY(cGetNome) .OR. EMPTY(cGetNreduz) .OR. EMPTY(cGetDDD) .OR. EMPTY(cGetNCel) .OR. EMPTY(cGetCPF))
           Alert("Existe campos vazios, por favor verifique!")
elseif MSGYESNO("Deseja mesmo excluir o registro de "+AllTrim(cGetNome)+"?","ATENCAO")


   DBSelectArea("SZ1")
   DBSetOrder(1)

   DbSeek(xFilial("SZ1")+cGetCod)

    Reclock("SZ1",.F.)
    DBDelete()
    MsUnlock("SZ1")

         if SZ1->Z1_SEXO = "1"
             MsgInfo("Protheuzeiro Excluido", "Sucesso")
         else
             MsgInfo("Protheuzeira Excluida", "Sucesso")
         endif
else
    Alert("Exclusao abortada pelo usuario","Atencao")  
endif

    DBCloseArea()

    LimpaTela()
    
Return

//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
//%%%%                                       Funcao Limpar a tela apos inclusao e exclusao   (LimpaTela)                                                    %%%%%%
//%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Static Function LimpaTela()
    
 //Dados     
              cGetCod       :=  Space(6)                     
              cGetNome      :=  Space(30)                     
              cGetNreduz    :=  Space(20)                     
              dGetDtNasc    :=  Date()
              cGetDDD       :=  Space(2)                     
              cGetNCel      :=  Space(6)                     
              nSexo         :=  2
              cGetCPF       :=  Space(14)

              //Endereco
              cGetEndereco  := Space(30)                     
              cGetBairro    := Space(20)                     
              cGetCEP       := Space(10)
              cGetCidade    := Space(30)                     
              cEstado       := aEstado[2]
              cImovel       := aImovel[2]

            //Outros   
              cEstCivil     :=  aEstCivil[1]
              nPet          :=  2
              lCheckAdvpl   := .F.
              lCheckCSHARP  := .F.
              lCheckSQL     := .F.
              lCheckPython  := .F.
              cObservacao   := "Digite aqui as caracteristicas de Protheuzeiro que te definem:"
 
Return 
